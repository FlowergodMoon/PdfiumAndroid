buildscript {
    repositories {
        google()
        mavenCentral()
        mavenLocal()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.3.0'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    namespace "com.shockwave.pdfium"

    compileSdk 34

    defaultConfig {
        minSdkVersion 23
        targetSdk 34
        versionCode 1
        versionName "1.9.1"
        buildConfigField 'String', 'VERSION_NAME', "\"${defaultConfig.versionName}\""
        externalNativeBuild {
            cmake {
                cppFlags ""
            }
        }
    }

    buildFeatures {
        buildConfig = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    publishing {
        publishing {
            singleVariant("release") {
                withSourcesJar()
                withJavadocJar()
            }
        }
    }

    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
            version "3.22.1"
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['src/main/jniLibs']
        }
    }
}

repositories {
    google()
    mavenCentral()
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.6.1'
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = 'com.github.lion1988dev'
            artifactId = 'com.shockwave.pdfium'
            version = "1.9.1"

            afterEvaluate {
                from components.release
            }
        }
    }
}